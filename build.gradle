plugins {
    id 'java'
    id 'de.undercouch.download' version '3.4.3'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}

group 'io.github.steakteam'

sourceCompatibility = 1.8

test.testLogging.showStandardStreams = true
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
compileJava.dependsOn clean

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'http://repo.dmulloy2.net/nexus/repository/public/' }
}

dependencies {
    compile('org.spigotmc:spigot-api:1.12-R0.1-SNAPSHOT') {
        exclude module: 'bungeecord-chat'
    }
    compile(group: 'com.comphenix.protocol', name: 'ProtocolLib-API', version: '4.4.0-SNAPSHOT') {
        exclude module: 'cglib-nodep'
    }
    compile group: 'cglib', name: 'cglib', version: '3.2.5'
    compile group: 'org.ow2.asm', name: 'asm', version: '5.2'
    compile group: 'io.netty', name: 'netty-buffer', version: '4.1.22.Final'
    compile fileTree('dir': 'libs', 'include': '*.jar')
}

shadowJar {
    classifier = ''
    dependencies {
        include dependency('cglib:cglib:3.2.5')
        include dependency('org.ow2.asm:asm:5.2')
    }
    relocate 'net.sf', "${project.group}.chplus.libs.net.sf"
    relocate 'org.objectweb', "${project.group}.chplus.libs.org.objectweb"
}

artifacts {
    archives shadowJar
}

task downloadLibs(type: Download) {
    mkdir 'libs'
    src 'http://builds.enginehub.org/job/commandhelper/last-successful/download/commandhelper-3.3.4-SNAPSHOT-full.jar'
    dest 'libs'
    onlyIfModified true
}

compileJava.dependsOn downloadLibs
